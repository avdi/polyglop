Room @end_of_road:
  Title: at end of road again
  Description:
    You are standing at the end of a road before a small brick building.
    Around you is a forest.  A small stream flows out of the building and
    down a gully.
  Exits:
    east to @building
    enter to @building
    south to @valley

Room @building:
  Title: inside building
  Description:
    You are inside a building, a well house for a large spring.
  Exits:
    west to @end_of_road
    exit to @end_of_road
  Objects:
    $keys
    $lamp
    $food
    $water_bottle

Object $keys:
  Terms: Set of keys, keys
  Description: There are some keys on the ground here.

Object $lamp:
  Terms: Brass lantern, brass lamp, lamp, lantern
  Description: There is a shiny brass lamp nearby.

Object $food:
  Terms: Tasty food, food
  Description: There is food here.

Object $water_bottle:
  Terms: Small bottle, bottle, water, water bottle
  Description: There is a bottle of water here.

Room @valley:
  Title: in valley
  Description:
    You are in a valley in the forest beside a stream tumbling along a
    rocky bed.
  Exits:
    north to @end_of_road
    south to @slit

Room @slit:
  Title: at slit in streambed
  Description:
    At your feet all the water of the stream splashes into a 2-inch slit
    in the rock.  Downstream the streambed is bare rock.
  Exits:
    north to @valley
    south to @depression

Room @depression:
  Title: outside grate
  Description:
    You are in a 20-foot depression floored with bare dirt.  Set into the
    dirt is a strong steel grate mounted in concrete.  A dry streambed
    leads into the depression.
  Exits:
    north to @slit
    enter to @grate_chamber guarded by:
      {{{
        if(blackboard.grate_unlocked) {
          true;
        } else {
          say("You can't go through a locked steel grate!");
          false;
        }
      }}}

Room @grate_chamber:
  Title: below the grate
  Description:
    You are in a small chamber beneath a 3x3 steel grate to the surface.
    A low crawl over cobbles leads inward to the west.
  Exits:
    exit to @depression

Action !unlock:
  Terms: unlock grate, open grate
  Code:
    {{{
      if(!playerIsIn('@depression')) {
         say("There is no grate here");
      } else if(!playerHasItem('$keys')) {
        say("You have no keys!");
      } else {
        say("The grate is now unlocked");
        blackboard.grate_unlocked = true;
      }
    }}}

Synonyms:
  north: n
  south: s
  east: e
  west: w
  look: l, examine
  take: get, pick up
  drop: put down
  inventory: i

